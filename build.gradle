buildscript {
    repositories {
        mavenLocal()
        maven { url "https://repository.jboss.org/" }
        maven { url "https://repo.grails.org/grails/core" }
        maven { url "https://nexus.ala.org.au/content/groups/public/" }

    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "org.grails.plugins:hibernate5:7.3.1"
        classpath "com.github.erdi:webdriver-binaries-gradle-plugin:3.0"
        classpath "com.bertramlabs.plugins:asset-pipeline-gradle:3.4.6"
        classpath 'org.grails.plugins:database-migration:4.2.0'
    }
}

plugins {
    id 'groovy'
    id 'java'
    id 'war'
    id("com.github.erdi.webdriver-binaries") version "3.0"

}

ext {
    // The drivers we want to use
    drivers = ["firefox", "chrome", "chromeHeadless"]
}

version "2.2.0-SNAPSHOT"
group "au.org.ala"

apply plugin: "eclipse"
apply plugin: "idea"
apply plugin: "war"
apply plugin: "org.grails.grails-web"
apply plugin: "com.github.erdi.webdriver-binaries"
apply plugin: "org.grails.grails-gsp"
apply plugin: "com.bertramlabs.asset-pipeline"


def alaSecurityLibsVersion='6.3.0'
def geotoolsVersion='27.2'

war {
    enabled = true
}

springBoot {
    mainClass = "au.org.ala.spatial.Application"
}

buildProperties {
    inputs.property("info.app.build.date", new Date().format('yyyy-MM-dd HH:mm:ss'))
}

repositories {
    mavenLocal()
    maven { url "https://nexus.ala.org.au/content/groups/public/" }
    maven { url "https://repo.osgeo.org/repository/release/" }
    maven { url "https://repo.grails.org/grails/core" }
    maven { url "https://repository.jboss.org/" }
    mavenCentral()
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
    implementation {
        //Avoid conflicting with 'org.hsqldb:hsqldb:2.4.1'
        exclude group: 'hsqldb', module: 'hsqldb'
    }
}

sourceSets {
    main {
        resources {
            srcDir 'grails-app/migrations'
        }
    }
}

dependencies {
    developmentOnly("org.springframework.boot:spring-boot-devtools")
    developmentOnly "org.springframework.boot:spring-boot-starter-logging"
    implementation "org.springframework.boot:spring-boot-autoconfigure"
    implementation "org.grails:grails-core"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-tomcat"
    implementation "org.grails:grails-web-boot"
    implementation "org.grails:grails-logging"
    implementation "org.grails:grails-plugin-rest"
    implementation "org.grails:grails-plugin-databinding"
    implementation "org.grails:grails-plugin-i18n"
    implementation "org.grails:grails-plugin-services"
    implementation "org.grails:grails-plugin-url-mappings"
    implementation "org.grails:grails-plugin-interceptors"
    implementation "org.grails.plugins:cache"
    implementation "org.grails.plugins:async"
    implementation "org.grails.plugins:scaffolding"
    implementation "org.grails.plugins:events"
    implementation "org.grails.plugins:hibernate5"
    implementation "org.hibernate:hibernate-core"
    implementation "org.hibernate:hibernate-spatial"
    implementation "org.grails.plugins:gsp"
    implementation "io.micronaut:micronaut-inject-groovy"

    console "org.grails:grails-console"
    profile "org.grails.profiles:web"
    runtimeOnly "com.h2database:h2"
    runtimeOnly "org.apache.tomcat:tomcat-jdbc"
    runtimeOnly "javax.xml.bind:jaxb-api:2.3.0"
    runtimeOnly "com.bertramlabs.plugins:asset-pipeline-grails:3.4.6"
    testImplementation "org.codehaus.groovy:groovy"
    testImplementation platform("org.spockframework:spock-bom:2.1-groovy-3.0")
    testImplementation "org.spockframework:spock-core"
    testImplementation "org.grails:grails-gorm-testing-support"
    testImplementation "org.mockito:mockito-core"
    testImplementation "org.grails:grails-web-testing-support"
    testImplementation "org.grails.plugins:geb"
    testImplementation "org.seleniumhq.selenium:selenium-remote-driver:3.14.0"
    testImplementation "org.seleniumhq.selenium:selenium-api:3.14.0"
    testImplementation "org.seleniumhq.selenium:selenium-support:3.14.0"
    testImplementation "org.seleniumhq.selenium:selenium-chrome-driver:3.14.0"
    testImplementation "org.seleniumhq.selenium:selenium-firefox-driver:3.14.0"
    testImplementation "com.github.erdi:webdriver-binaries-gradle-plugin:3.0"

    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    implementation 'org.grails.plugins:converters'
    implementation 'javax.media:jai_core:1.1.3'
    implementation 'org.apache.httpcomponents:httpclient:4.5.3'

    // plugins
    implementation("org.quartz-scheduler:quartz:2.2.3") {
        exclude group: 'slf4j-api', module: 'c3p0'
    }
    implementation 'org.grails.plugins:quartz:2.0.13'
    implementation 'commons-fileupload:commons-fileupload:1.4'
    implementation group: 'commons-httpclient', name: 'commons-httpclient', version: '3.1'

    // plugins needed at runtime but not for compilation
    implementation "org.grails.plugins:ala-admin-plugin:2.3.0"
    implementation ("org.grails.plugins:ala-bootstrap3:4.4.0") {
        exclude module: 'ala-cas-client'
    }

    implementation "org.grails.plugins:ala-auth:$alaSecurityLibsVersion"
    implementation "org.grails.plugins:ala-ws-plugin:$alaSecurityLibsVersion"
    implementation "org.grails.plugins:ala-ws-security-plugin:$alaSecurityLibsVersion"
    implementation "au.org.ala:userdetails-service-client:$alaSecurityLibsVersion"

//    implementation 'org.grails.plugins:ala-cors-plugin:0.1-SNAPSHOT'
    implementation 'au.org.ala.plugins:openapi:1.3.0'

    //implementation 'org.grails.plugins:mail:2.0.0.RC6'

    implementation("org.xhtmlrenderer:core-renderer:R8") {
        exclude module: "bcprov-jdk14"
        exclude module: "bcmail-jdk14"
    }
    implementation("com.lowagie:itext:2.1.0") {
        exclude module: "bcprov-jdk14"
        exclude module: "bcmail-jdk14"
    }
    implementation('org.grails.plugins:rendering:2.0.3') {
        exclude module: "bcprov-jdk14"
        exclude module: "bcmail-jdk14"
    }

    implementation 'dk.glasius:external-config:3.1.1'

    // dependencies
    implementation "org.codehaus.jackson:jackson-core-asl:1.8.6"
    implementation "org.codehaus.jackson:jackson-mapper-asl:1.8.6"


    // https://mvnrepository.com/artifact/org.postgresql/postgresql
    implementation group: 'org.postgresql', name: 'postgresql', version: '42.7.3'
    implementation group: 'net.postgis', name: 'postgis-jdbc', version: '2023.1.0'

    implementation 'jfree:jfreechart:1.0.13'
    implementation 'com.thoughtworks.xstream:xstream:1.4.20' // TODO Upgrade to latest version due to CVEs
    implementation 'commons-io:commons-io:2.7'
    testImplementation 'org.apache.ant:ant:1.9.16'

    implementation 'com.github.ziplet:ziplet:2.0.0'

    //implementation 'org.springframework.boot:spring-boot-starter-data-redis:2.0.0.RELEASE'
    //implementation 'org.springframework.session:spring-session:1.3.5.RELEASE'

    // db-migration
    // In newer versions use 'implementation'
//    implementation 'org.liquibase:liquibase-core:3.10.3'
//    implementation "org.grails.plugins:database-migration:3.1.0"

    implementation 'org.codehaus.janino:janino:2.5.16'
    implementation "org.geotools:gt-main:$geotoolsVersion"
    implementation "org.geotools:gt-shapefile:$geotoolsVersion"
    implementation "org.geotools:gt-svg:$geotoolsVersion"
    implementation "org.geotools.xsd:gt-xsd-kml:$geotoolsVersion"
    implementation "org.geotools:gt-render:$geotoolsVersion"
    implementation "org.geotools:gt-wms:$geotoolsVersion"
    implementation "org.geotools:gt-geojson:$geotoolsVersion"
    implementation "org.geotools:gt-epsg-hsql:$geotoolsVersion"
    implementation "org.geotools:gt-imageio-ext-gdal:$geotoolsVersion"

    implementation "com.opencsv:opencsv:3.7"

//    developmentOnly 'io.methvin:directory-watcher:0.15.0'

    implementation 'org.gbif:dwc-api:1.47'
}

configurations {
    all {
        exclude group: "org.slf4j", module: "slf4j-nop"
    }
}

bootRun {
    jvmArgs(
            '-Dspring.output.ansi.enabled=always',
            '-noverify',
            '-XX:TieredStopAtLevel=1',
            '-Xmx2048m')
    sourceResources sourceSets.main
    String springProfilesActive = 'spring.profiles.active'
    systemProperty springProfilesActive, System.getProperty(springProfilesActive)
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

assets {
    minifyJs = true
    minifyCss = true
    packagePlugin = true
}

ext {
    // The drivers we want to use
    drivers = ["firefox", "chrome", "chromeHeadless"]
}

bootRun {
    ignoreExitValue true
    jvmArgs(
            '-Dspring.output.ansi.enabled=always',
            '-noverify',
            '-XX:TieredStopAtLevel=1',
            '-Xmx1024m')
    sourceResources sourceSets.main
    String springProfilesActive = 'spring.profiles.active'
    systemProperty springProfilesActive, System.getProperty(springProfilesActive)
}

//webdriverBinaries {
//    chromedriver {
//        versionRegexp = '.*'
//    }
//    geckodriver {
//        versionRegexp = '.*'
//    }
//}

tasks.getByName("integrationTest") {
    group JavaBasePlugin.VERIFICATION_GROUP

    //outputs.upToDateWhen { false }  // Always run tests

    systemProperty "geb.build.reportsDir", reporting.file("geb/$name")

    systemProperty "geb.env", System.properties['driver'] ?: "firefox"  // firefox by default
    // pass test server by -DbaseUrl
    systemProperty "geb.build.baseUrl", System.properties['baseUrl'] ?: "http://devt.ala.org.au:8081/ws"
    //Store username and password into the default config file
    systemProperty "configFile", System.properties['configFile'] ?: "/data/spatial-service/test/default.properties"
    //or pass with -Dusername, -Dpassword
    systemProperty "username", System.properties['username']
    systemProperty "password", System.properties['password']
}

tasks.withType(Test) {
    maxHeapSize = "1g"
    jvmArgs '-XX:MaxMetaspaceSize=256m'
    testLogging {
        exceptionFormat = 'full'
    }
}

// Refer to FieldCapture: https://github.com/AtlasOfLivingAustralia/fieldcapture/blob/feature/grails4/gradle/publish.gradle
apply plugin: "maven-publish"
publishing {
    repositories {
        maven {
            name 'Nexus'
            url "https://nexus.ala.org.au/content/repositories/${project.version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases'}"
            credentials {
                username = System.getenv('TRAVIS_DEPLOY_USERNAME')
                password = System.getenv('TRAVIS_DEPLOY_PASSWORD')
            }
        }
    }

    publications {
        MavenPublication(MavenPublication) {
            pom.withXml {
                def pomNode = asNode()
                pomNode.dependencyManagement.replaceNode {}

                // simply remove dependencies without a version
                // version-less dependencies are handled with dependencyManagement
                // see https://github.com/spring-gradle-plugins/dependency-management-plugin/issues/8 for more complete solutions
                pomNode.dependencies.dependency.findAll {
                    it.version.text().isEmpty()
                }.each {
                    it.replaceNode {}
                }
            }
            artifact bootWar
        }
    }
}
